#
# This file is part of the Boomerang Decompiler.
#
# See the file "LICENSE.TERMS" for information on usage and
# redistribution of this file, and for a DISCLAIMER OF ALL
# WARRANTIES.
#

if (WIN32)
    FLEX_TARGET(AnsiCScanner
                ${CMAKE_CURRENT_SOURCE_DIR}/AnsiCScanner.l
                ${CMAKE_CURRENT_SOURCE_DIR}/AnsiCScanner.cpp
                COMPILE_FLAGS --wincompat
    )
else (WIN32)
    FLEX_TARGET(AnsiCScanner
                ${CMAKE_CURRENT_SOURCE_DIR}/AnsiCScanner.l
                ${CMAKE_CURRENT_SOURCE_DIR}/AnsiCScanner.cpp
    )
endif (WIN32)


if (NOT FLEX_AnsiCScanner_DEFINED)
    message(FATAL_ERROR "Could not generate ANSI C scanner")
endif (NOT FLEX_AnsiCScanner_DEFINED)


BISON_TARGET(AnsiCParser
             ${CMAKE_CURRENT_SOURCE_DIR}/AnsiCParser.y
             ${CMAKE_CURRENT_SOURCE_DIR}/AnsiCParser.cpp
)


if (NOT BISON_AnsiCParser_DEFINED)
    message(FATAL_ERROR "Could not generate ANSI C parser")
endif (NOT BISON_AnsiCParser_DEFINED)


ADD_FLEX_BISON_DEPENDENCY(AnsiCScanner AnsiCParser)

add_library(boomerang-ansic-parser STATIC
    ${BISON_AnsiCParser_OUTPUTS}
    ${FLEX_AnsiCScanner_OUTPUTS}
    AnsiCParserDriver.cpp
    AnsiCParserDriver.h
)

if (CMAKE_COMPILER_IS_GNUCXX AND BISON_VERSION VERSION_LESS 3.0.5)
    # Grammars generated by Bison 3.0.4 and below emit a null pointer dereference warning
    # when compiled with GCC 7
    set_source_files_properties(${CMAKE_CURRENT_SOURCE_DIR}/AnsiCParser.cpp
        PROPERTIES COMPILE_FLAGS -Wno-error=null-dereference)
endif ()

target_link_libraries(boomerang-ansic-parser Qt5::Core)
